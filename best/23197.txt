#ifdef GL_ES
precision mediump float;
#endif

/*
VRG corp indahood

Watch with:
http://lackluster.bandcamp.com/album/slice

*/

uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

uniform sampler2D bb;

#define PI 3.14159265359

#define PETALS 7.
#define PARTS (PETALS*.5/PI)
#define SPEED -.25
#define SCALE 3.

// http://lolengine.net/blog/2013/07/27/rgb-to-hsv-in-glsl
vec3 hsv2rgb(vec3 c){
    vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
    vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
    return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

vec2 c2p(vec2 p){
	return vec2( length(p), atan(p.y, p.x));
}

vec2 p2c(vec2 p){
	return vec2(cos(p.y)*p.x, sin(p.y)*p.x);
}

vec3 getColor(vec2 p){
	float pxModed = mod(p.x+time*SPEED, 1.);
	
	
	float inPetals = smoothstep( pxModed, pxModed +.025, abs(p.y));
	
	float ballA = 1.-step(.05*p.x - step(p.x, .21), length(p2c(vec2(pxModed, (p.y+.5)*p.x*1.5) ) - vec2(.55, 0.)));
	float ballB = 1.-step(.05*p.x - step(p.x, .21), length(p2c(vec2(pxModed, (p.y-.5)*p.x*1.5) ) - vec2(.55, 0.)));
	float balls = max(ballA, ballB);
	
	inPetals = max(inPetals, balls);
	
	vec3 color_in = hsv2rgb(vec3(floor(p.x+time*SPEED)/(8.), 1., 1.))*(1.-smoothstep(.01, 0., 1. -pxModed));
	vec3 color_out = vec3(step(0.,cos(p.y*(20.*p.x))));
	color_out = mix(color_out, vec3(1.) - color_out, balls);
	
	return mix(color_in, color_out, inPetals);
}

void main( void ) {
	float ratio = resolution.x/resolution.y;
	vec2 p = gl_FragCoord.xy / resolution.xy;
	p-=vec2(.5);
	p.x*=ratio;
	
	vec2 polar = c2p(p);
	polar.x*=SCALE;
	polar.y+=time*SPEED*.5;
	
	polar.y = mod(polar.y, 1./PARTS)*PARTS;
	polar.y-=.5;
	

	vec3 color = getColor(polar);
	gl_FragColor = vec4( color, 1.0 );

}