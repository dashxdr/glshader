// Cloudy with a chance of Metaballs
// Just Medo messing around at Revision 2014
// Now with colors! :P

#ifdef GL_ES
precision mediump float;
#endif

uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

vec3 ballcolor(int i)
{
	return vec3(sin(float(i)*.23), sin(float(i)*.98), sin(float(i)*.83));
}

vec3 ballpos(int i)
{
	return vec3(3.*sin(time*sin(float(i))), 3.*cos(time*cos(float(i)*.43)), 10.+3.*sin(time*-sin(float(i)*0.3)));	
}

float d(vec3 pos)
{
	vec3 tdir = vec3(cos(time*.1), 0., sin(time*.1));
	vec3 tdir2 = vec3(-sin(time*.1), 0., cos(time*.1));
	
	float d = 0.;
	for(int i=0; i<10; i++)
		d += 1./(length(pos-ballpos(i))+0.5);
	
	return (/*min(min(length(pos-vec3(0.,.1*sin(time*5.),10.))-1.+.1*sin(pos.y*5.)*sin(40.*atan(dot(pos, tdir), dot(pos, tdir2))),
		   20.-pos.y),*/
		   1./d*7.5-3.0);
}

vec3 march(vec3 pos, vec3 dir)
{
	for(int i=0; i<40; i++)
	{
		float dist = d(pos);
		pos += dir*dist*0.9;
	}
	return pos;
}

void main( void ) {
	vec3 ls = vec3(sin(time)*50., 5., -20.);
	vec3 v = normalize(vec3(((gl_FragCoord.xy - resolution.xy/2.)/min(resolution.x, resolution.y)), 1));
	vec3 scp = march(vec3(0.), v);
	float ds = d(scp);
	vec3 norm = normalize(vec3(ds-d(scp+vec3(0.01,0.,0.)), ds-d(scp+vec3(0.,0.01,0.)), ds-d(scp+vec3(0.,0.,0.01))));
	float bright = max(0., 8./length(scp-ls) * dot(norm, normalize(scp-ls)));
	float specular = max(0., .4*pow(dot(reflect(-v, norm), normalize(scp-ls)), 11.));
	vec3 color = vec3(0.);
	for(int i=0; i<10; i++)
	{
		color += ballcolor(i)/(10.+pow(length(ballpos(i)-scp),4. ));
	}
	gl_FragColor = (.2+bright+specular)*vec4( color*106./length(scp), 1.0 );
}
